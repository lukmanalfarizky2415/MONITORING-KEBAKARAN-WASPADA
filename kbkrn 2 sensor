#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include "DHT.h"

#define MQ2_PIN A0
#define FLAME_PIN 2
#define DHTPIN 7
#define DHTTYPE DHT11

#define LED_GAS 8
#define LED_FLAME 9

LiquidCrystal_I2C lcd(0x27, 16, 2);
DHT dht(DHTPIN, DHTTYPE);

const int MQ_THRESHOLD = 400;

void setup() {
  pinMode(MQ2_PIN, INPUT);
  pinMode(FLAME_PIN, INPUT);
  pinMode(LED_GAS, OUTPUT);
  pinMode(LED_FLAME, OUTPUT);

  Serial.begin(9600);
  dht.begin();

  lcd.init();
  lcd.backlight();

  lcd.setCursor(0, 0);
  lcd.print("Sistem Siaga");
  lcd.setCursor(0, 1);
  lcd.print("Kebakaran");
  delay(2000);
  lcd.clear();
}

void loop() {
  int mq2_value = analogRead(MQ2_PIN);
  int flameDetected = digitalRead(FLAME_PIN);
  float temperature = dht.readTemperature();
  float humidity = dht.readHumidity();

  bool gasAlarm = (mq2_value > MQ_THRESHOLD);
  bool flameAlarm = (flameDetected == HIGH);

  digitalWrite(LED_GAS, gasAlarm ? HIGH : LOW);
  digitalWrite(LED_FLAME, flameAlarm ? HIGH : LOW);

  lcd.clear();
  lcd.setCursor(0, 0);
  if (flameAlarm) {
    lcd.print("API TERDETEKSI");
  } else if (gasAlarm) {
    lcd.print("GAS TERDETEKSI");
  } else {
    lcd.print("Sistem Aman   ");
  }

  lcd.setCursor(0, 1);
  if (!isnan(temperature) && !isnan(humidity)) {
    lcd.print("T:");
    lcd.print(temperature, 1);
    lcd.print("C H:");
    lcd.print(humidity, 0);
    lcd.print("%  ");
  } else {
    lcd.print("Sensor Error  ");
  }

  Serial.print("MQ2: ");
  Serial.print(mq2_value);
  Serial.print(" | Flame: ");
  Serial.print(flameDetected);
  Serial.print(" | Temp: ");
  Serial.print(temperature);
  Serial.print(" | Humidity: ");
  Serial.println(humidity);

  delay(2000);
}
